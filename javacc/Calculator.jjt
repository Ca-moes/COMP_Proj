options
{
    LOOKAHEAD=1;
}

PARSER_BEGIN(Calculator)

public class Calculator {
    public static void main(String[] args) throws ParseException {
        Calculator myCalc = new Calculator(System.in);
        SimpleNode root = myCalc.Program(); // returns reference to root node
        root.dump("");  // prints the tree on the screen
    }
}

PARSER_END(Calculator)

SKIP :
{
    " " | "\t" | "\r" | "\n"
}

TOKEN :
{
     < IMPORT : "import" >
    | < DOT : ".">
    | < SEMICOLON : ";">
    | < CLASS : "class">
    | < EXTENDS : "extends">
    | < OPENCURLY : "{">
    | < CLOSECURLY : "}">
    | < PUBLIC : "public">
    | < OPENPAR : "(">
    | < CLOSEPAR : ")">
    | < COMMA : ",">
    | < RETURN : "return">
    | < STATIC : "static">
    | < VOID : "void">
    | < MAIN : "main">
    | < STRING : "String">
    | < OPENBRACKET : "[">
    | < CLOSEBRACKET : "]">
    | < INT : "int">
    | < BOOL : "boolean">
    | < IF : "if">
    | < ELSE : "else">
    | < WHILE : "while">
    | < EQUAL : "=">
    | < AND : "&&">
    | < LESSTHAN : "<">
    | < PLUS : "+">
    | < MINUS : "-">
    | < MULT : "*">
    | < DIV : "/">
    | < TRUE : "true">
    | < FALSE : "false">
    | < THIS : "this">
    | < NEW : "new">
    | < NOT : "!">
    | < LENGTH : "length" >
    | < INTEGERLITERAL : (["0"-"9"])+>
    | < IDENTIFIER : ["a"-"z", "A"-"Z", "$", "_"](["a"-"z", "A"-"Z", "0"-"9", "$", "_"])* >
}

SimpleNode Program(): {}
{
  (ImportDeclaration())* ClassDeclaration() <EOF> { return jjtThis; }
}

void ImportDeclaration() : {}
{
  <IMPORT> <IDENTIFIER> (<DOT> <IDENTIFIER>)* <SEMICOLON>
}

void ClassDeclaration() : {}
{
  <CLASS> <IDENTIFIER> [<EXTENDS> <IDENTIFIER>] <OPENCURLY> (VarDeclaration())* (MethodDeclaration())* <CLOSECURLY>
}

void VarDeclaration() : {}
{
  Type() <IDENTIFIER> <SEMICOLON>
}

void Type() #void : {}
{
  <BOOL> | <INT> [<OPENBRACKET><CLOSEBRACKET>] | <IDENTIFIER>
}

void MethodDeclaration() #void : {}
{
  <PUBLIC> MD1()
}

void Param() : {}
{
  Type() <IDENTIFIER>
}

void Return() : {}
{
  <RETURN> Expression() <SEMICOLON>
}

void MD1() #void : {}
{
  (Type() <IDENTIFIER> <OPENPAR> [Param() (<COMMA> Param())*] <CLOSEPAR> <OPENCURLY> (LOOKAHEAD(2) VarDeclaration())* (Statement())* Return()<CLOSECURLY>) #ClassMethod |
  (<STATIC> <VOID> <MAIN> <OPENPAR> <STRING> <OPENBRACKET> <CLOSEBRACKET> <IDENTIFIER> <CLOSEPAR> <OPENCURLY> (LOOKAHEAD(2) VarDeclaration())* (Statement())* <CLOSECURLY>) #MainMethod
}

void Statement() #void : {}
{
  (<OPENCURLY> (Statement())* <CLOSECURLY>) |
  (<IF> <OPENPAR> Expression() <CLOSEPAR> Statement() <ELSE> Statement()) #IfElse |
  (<WHILE> <OPENPAR> Expression() <CLOSEPAR> Statement()) #While |
  LOOKAHEAD(2) (<IDENTIFIER> #Variable [<OPENBRACKET> Expression() <CLOSEBRACKET>] <EQUAL> Expression() <SEMICOLON>) #Assignment |
  (Expression() <SEMICOLON>)
}

void Expression() #void : {}
{
  Term() |
  Variable() |
  Init() |
  Not() |
  EnclosedExpression()
}

void Term() : {}
{
  (
    <INTEGERLITERAL> |
    <TRUE> |
    <FALSE>
  ) Exp1()
}

void Variable() : {}
{
  (
    <IDENTIFIER> |
    <THIS>
  ) Exp1()
}

void Init() #void : {}
{
  <NEW>
  (
    ArrayInit() |
    ObjInit()
  ) Exp1()
}

void ArrayInit() : {}
{
  <INT> <OPENBRACKET> Expression() <CLOSEBRACKET>
}

void ObjInit() : {}
{
  <IDENTIFIER> <OPENPAR> <CLOSEPAR>
}

void Not() : {}
{
  <NOT> Expression() Exp1()
}

void EnclosedExpression() : {}
{
  <OPENPAR> Expression() <CLOSEPAR> Exp1()
}

void Exp1() #void : {}
{
  [
    LOOKAHEAD(2)
    (
      BinaryOperation() |
      ArrayAccess() |
      MethodCall()
    )
  ]
}

void BinaryOperation() : {}
{
  (<AND> | <LESSTHAN> | <PLUS> | <MINUS> | <MULT> | <DIV>) Expression() Exp1()
}

void ArrayAccess() : {}
{
  <OPENBRACKET> Expression() <CLOSEBRACKET> Exp1()
}

void MethodCall() : {}
{
  <DOT> Method() Exp1()
}

void Method() #void : {}
{
  <LENGTH> |
  (<IDENTIFIER> <OPENPAR> [Expression() (<COMMA> Expression())*] <CLOSEPAR>)
}
